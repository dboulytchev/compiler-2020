-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "state".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |
--        if     (expr, stmt, stmt) |
--        While  (expr, stmt)       |
--        Repeat (stmt, expr)

fun eval (state@[st, w], stmt) {
  case stmt of
    Skip          -> state
  | Assn(x, val)  -> [st <- [x, evalExpr(st, val)], w]
  | Seq(st1, st2) -> eval(eval(state, st1), st2)
  | Read(x)       -> case readWorld(w) of [val, w2] -> [st <- [x, val], w2] esac
  | Write(val)    -> [st, writeWorld(evalExpr(st, val), w)]
  | If(e, s1, s2) -> 
      if   evalExpr(st, e) != 0
      then eval(state, s1)
      else eval(state, s2)
      fi
  | cycle@While(cond, body) ->
      if   evalExpr(st, cond) != 0
      then eval(eval(state, body), cycle)
      else state
      fi
  | Repeat(body, cond) -> 
      eval(eval(state, body), While(Binop("==", cond, Const(0)), body))
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
