-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |


fun eval (c@[st, w], stmt) {
  case stmt of
    Read(x)       -> case readWorld(w) of [val, w2] -> [st <- [x, val], w2] esac
  | Write(val)    -> [st, writeWorld(evalExpr(st, val), w)]
  | Seq(st1, st2) -> eval(eval(c, st1), st2)
  | Assn(x, val)  -> [st <- [x, evalExpr(st, val)], w]
  | Skip          -> c
  | _             -> failure ("Unexpected error in eval\n")
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
