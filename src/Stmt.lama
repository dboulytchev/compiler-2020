-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
fun eval (c@[state, world], stmt) {
  case stmt of
    Seq (a, b)  -> eval (eval (c, a), b)
  | Read (a)    -> local ret = readWorld (world); 
                   [state <- [a, ret[0]], ret[1]]
  | Write (e)   -> [state, writeWorld (evalExpr (state, e), world)]
  | Assn (a, b) -> [state <- [a, evalExpr (state, b)], world]
  | _           -> c
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
