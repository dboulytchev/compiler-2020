-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
fun eval (c @ [state, world], stmt) {
  case stmt of
      Skip -> c
    | Read (x) -> case readWorld (world) of
                    [value, newworld] -> [state <- [x, value], newworld]
                  esac
    | Write (e) -> [state, writeWorld(state.evalExpr(e), world)]
    | Assn (x, e) -> [state <- [x, state.evalExpr(e)], world]
    | Seq (l, r) -> c.eval(l).eval(r)
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}