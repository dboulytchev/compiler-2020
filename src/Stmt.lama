-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |


fun eval (c@[s, w], stmt) {
  case stmt of
      Skip -> c
    | Seq(stmt1, stmt2) -> eval(eval([s, w], stmt1), stmt2)
    | Read(x) -> case readWorld(w) of 
        [val, w2] -> [s <- [x, val], w2]
      esac
    | Write(val) -> [s, writeWorld(evalExpr(s, val), w)]
    | Assn(x, val) -> [s <- [x, evalExpr(s, val)], w]
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
