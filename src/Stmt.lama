-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |
--        if     (expr, stmt, stmt) |
--        While  (expr, stmt)       |
--        Repeat (stmt, expr)

fun eval (c@[st, w], stmt) {
  case stmt of
    Skip          -> c
  | Assn(x, val)  -> [st <- [x, evalExpr(st, val)], w]
  | Seq(st1, st2) -> eval(eval(c, st1), st2)
  | Read(x)       -> case readWorld(w) of [val, w2] -> [st <- [x, val], w2] esac
  | Write(val)    -> [st, writeWorld(evalExpr(st, val), w)]
  | If(e, s1, s2) -> 
      if   evalExpr(st, e) != 0
      then eval(c, s1)
      else eval(c, s2)
      fi
  | cycle@While(cond, body) ->
      if   evalExpr(st, cond) != 0
      then eval(eval(c, body), cycle)
      else c
      fi
  | Repeat(body, cond) -> 
      eval(eval(c, body), While(Binop("==", cond, Const(0)), body))
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
