-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |
--        If     (expr, stmt, stmt) |
--        While  (expr, stmt)       |
--        Repeat (stmt, expr)

fun eval (state@[st, w], stmt) {
    case stmt of
            Read(x) -> case readWorld(w) of [val, new_w] -> [st <- [x, val], new_w] esac
        |   Write(e) -> [st, writeWorld(evalExpr(st, e), w)]
        |   Assn(x, e) -> [st <- [x, evalExpr(st, e)], w]
        |   Seq(s1, s2) -> eval(eval(state, s1), s2)
        |   Skip -> state
        |   If(e, s1, s2) -> if evalExpr(st, e) then eval(state, s1) else eval(state, s2) fi
        |   While(e, s) -> if evalExpr(st, e) then eval(eval(state, s), stmt) else state fi
        |   Repeat(s, e) -> 
              case eval(state, s) of
                new_state@[new_st, _] -> 
                  if evalExpr(new_st, e) == 0
                    then eval(new_state, stmt)
                    else new_state
                  fi
              esac
    esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
