-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |
--        if     (expr, stmt, stmt) |
--        While  (expr, stmt)       |
--        Repeat (stmt, expr)

fun eval ([st, w], stmt) {
  case stmt of
      Assn(x, val) -> [st <- [x, evalExpr(st, val)], w]
    | Seq(stmt1, stmt2) -> eval(eval([st, w], stmt1), stmt2)
    | Skip -> [st, w]
    | Read(x) -> case readWorld(w) of 
        [val, w2] -> [st <- [x, val], w2]
      esac
    | Write(val) -> [st, writeWorld(evalExpr(st, val), w)]
    | If(expr, stmt1, stmt2) -> if evalExpr(st, expr) != 0 then eval([st, w], stmt1) else eval([st, w], stmt2) fi
    | While(expr, stmt1) -> if evalExpr(st, expr) != 0 then eval(eval([st, w], stmt1), stmt) else [st, w] fi
    | Repeat(stmt1, expr) -> case eval([st, w], stmt1) of
        [new_st, new_w] -> if evalExpr(new_st, expr) != 0 then [new_st, new_w] else eval([new_st, new_w], stmt) fi
      esac
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
