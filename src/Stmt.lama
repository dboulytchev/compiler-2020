-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |
--        if     (expr, stmt, stmt) |
--        While  (expr, stmt)       |
--        Repeat (stmt, expr)

fun eval (c@[st, w], stmt) {
  case stmt of 
    Assn(x, expr) -> [st <- [x, evalExpr(st, expr)], w]
  | Seq(st1 ,st2) -> eval(eval(c, st1), st2)
  | Skip -> c
  | Read(str) -> case readWorld(w) of
                   [n, w] -> [st <- [str, n], w]
                 esac
  | Write(expr) -> [st, writeWorld(evalExpr(st, expr), w)]
  | If (expr, st1, st2) -> if evalExpr(st, expr) != 0 then eval([st, w], st1) else eval([st, w], st2) fi
  | While (expr, st1) -> if evalExpr(st, expr) != 0 then eval(eval([st, w], st1), stmt) else [st, w] fi
  | Repeat (st1, expr) -> case eval([st, w], st1) of
                              [statep, worldp]-> if evalExpr(statep, expr) != 0 then [statep, worldp] else eval([statep, worldp], stmt) fi
                            esac
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
