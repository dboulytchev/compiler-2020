-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)


public fun binOp(op, left, right) {
  case op of
      "+"  -> left +  right
    | "-"  -> left -  right
    | "*"  -> left *  right
    | "/"  -> left /  right
    | "%"  -> left %  right
    | "==" -> left == right
    | "!=" -> left != right
    | "<"  -> left <  right
    | "<=" -> left <= right
    | ">"  -> left >  right
    | ">=" -> left >= right
    | "&&" -> left && right
    | "!!" -> left !! right
  esac
}

public fun evalExpr (st, expr) {
  case expr of
    Binop (op, left, right)   -> binOp(op, evalExpr(st, left), evalExpr(st, right))
    | Const (val)             -> val
    | Var   (val)             -> st(val)
    | _                       -> failure ("evalExpr not implemented\n")
  esac
}