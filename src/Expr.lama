-- Expression evaluator

import List;
import State;

-- Implementation of binary operation
public fun binOp(op, left, right) {
  case op of
    "+"  -> left +  right
  | "-"  -> left -  right
  | "*"  -> left *  right
  | "/"  -> left /  right
  | "%"  -> left %  right
  | "==" -> left == right
  | "!=" -> left != right
  | "<"  -> left <  right
  | "<=" -> left <= right
  | ">"  -> left >  right
  | ">=" -> left >= right
  | "&&" -> left && right
  | "!!" -> left !! right
  esac
}

-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun evalExpr (state, expr) {
  case expr of
    Binop (op, left, right) -> binOp(op, evalExpr(state, left), evalExpr(state, right))
  | Const (val)             -> val
  | Var   (val)             -> state(val)
  | _                       -> failure("Wrong Expr")
  esac
}
