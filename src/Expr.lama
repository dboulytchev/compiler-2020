-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
local ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn   (string, expr)       |
--        Seq    (expr, expr)         |
--        Skip                        |
--        Read   (string)             |
--        Write  (expr)               |
--        if     (expr, expr, expr)   |
--        While  (expr, expr)         |
--        Repeat (expr, expr)         |
--        Var    (string)             |
--        Ref    (string)             |
--        Const  (int)                |
--        Binop  (string, expr, expr) |
--        Scope  (def list, expr)     |
--        Call   (string, expr list)  |
--        Ignore (expr)
--
-- A definition is either a local variable definition or a function
-- definition:
--
-- def = Local (string list)                |
--       Fun   (string, string list, expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, val] -> [c, val : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

public fun eval (c@[state, world], expr) {
  case expr of
    Var (a)          -> [c, checkVar (state, a); case lookup (state, a) of Var (a) -> a esac]
  | Const (a)        -> [c, a]
  | Binop (op, a, b) -> case evalList (c, {a, b}) of
                          [c, a : b : {}] -> [c, evalOp (op, a, b)]
                        esac
  | Assn (a, b)      -> case evalList (c, {a, b}) of
                          [[state, world], Ref (a) : b : {}] -> [[state <- [a, Var (b)], world], b]
                        esac
  | Ref (a)          -> [c, Ref (a)]
  | Seq (a, b)       -> case eval (c, a) of
                          [c, _] -> eval (c, b)
                        esac
  | Skip             -> [c, Void]
  | Read (Ref (a))   -> case readWorld (world) of 
                          [n, world] -> [[state <- [a, Var (n)], world], Void]
                        esac
  | Write (e)        -> case eval (c, e) of
                          [[state, world], v] -> [[state, writeWorld (v, world)], Void]
                        esac
  | If (e, s1, s2)   -> case eval (c, e) of
                          [c, n] -> if n != 0 then eval (c, s1) else eval (c, s2) fi
                        esac
  | While (e, s)     -> case eval (c, e) of
                          [c, n] -> if n != 0 then [eval (eval (c, s) [0], expr) [0], Void] else [c, Void] fi
                        esac
  | Repeat (s, e)    -> case eval (c, s) of
                          [c, _] -> eval (c, While (Binop ("==", e, Const (0)), s))
                        esac
  | Ignore (e)       -> case eval (c, e) of
                          [c, _] -> [c, Void]
                        esac
  | Scope (ds, e)    -> case eval ([foldl (fun (state, x) { 
                                             case x of 
                                               Fun (name, args, body) -> addFunction (state, name, args, body) 
                                             | Local (names)          -> addNames (state, names)
                                             esac
                                           }, enterScope (state), ds), world], e) of
                          [[state, world], res] -> [[leaveScope (state), world], res]
                        esac
  | Call (f, es)     -> case checkFun (state, f); lookup (state, f) of
                          Fun (args, body) -> 
                            case evalList (c, es) of    
                              [[state, world], es] -> 
                                case eval ([foldl (fun (state, [name, value]) { state <- [name, Var (value)] }, addNames (enterFunction (state), args), zip (args, es)), world], body) of
                                  [[newState, world], res] -> [[leaveFunction (state, getGlobal (newState)), world], res]
                                esac
                            esac
                        esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
