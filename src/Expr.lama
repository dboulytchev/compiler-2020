-- Expression evaluator

import List;
import State;

-- Implementation of binary operation
public fun evalBinop(op, l, r) {
  case op of
    "*"  -> l *  r
  | "/"  -> l /  r
  | "%"  -> l %  r
  | "+"  -> l +  r
  | "-"  -> l -  r
  | "==" -> l == r
  | "!=" -> l != r
  | "<"  -> l <  r
  | "<=" -> l <= r
  | ">"  -> l >  r
  | ">=" -> l >= r
  | "&&" -> l && r
  | "!!" -> l !! r
  | _    -> failure ("Unexpected error in evalBinop\n")
  esac
}

-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun evalExpr (st, expr) {
  case expr of
    Binop(o, l, r) -> evalBinop(o, evalExpr(st, l), evalExpr(st, r))
  | Const(x)       -> x
  | Var(x)         -> st(x)
  esac
}
