-- Expression evaluator

import List;
import State;

public fun evalBinop (op, val_l, val_r) {
    case op of
        "+" -> val_l + val_r
    |   "-" -> val_l - val_r
    |   "*" -> val_l * val_r
    |   "/" -> val_l / val_r
    |   "%" -> val_l % val_r
    |   "==" -> val_l == val_r
    |   "!=" -> val_l != val_r
    |   "<" -> val_l < val_r
    |   "<=" -> val_l <= val_r
    |   ">" -> val_l > val_r
    |   ">=" -> val_l >= val_r
    |   "&&" -> val_l && val_r
    |   "!!" -> val_l !! val_r
    esac
}

-- The evaluator itself: takes a state and an expression,
-- returns integer value
public fun evalExpr (st, expr) {
    case expr of
        Const (x) -> x
    |   Var (x) -> st(x)
    |   Binop(op, expr_l, expr_r) -> evalBinop(op, evalExpr(st, expr_l), evalExpr(st, expr_r))
    esac
}
