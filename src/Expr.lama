-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
local ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        Repeat  (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, val] -> [c, val : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}


fun evalDefs(s, defs) {
  foldl (fun (s, def) {
              case def of
                Local (xs) -> addNames(s, xs)
              | Fun (f, args, e) -> addFunction(s, f, args, e)  
              esac
            }, s, defs
        )
}

fun addNamedValues (state, names, vals) {
  foldl (fun (s, [name, val]) {s.addName (name, Val (val))}, state, zip(names, vals))
}

fun eval (c@[s, w], expr) {
  case expr of
    Const (x) -> [c, x]
  | Var (x) -> [c, case lookupVal (s, x) of Val (v) -> v esac]
  | Ref (x) -> lookupVal (s, x); [c, Ref (x)]
  | Binop (op, l, r) -> 
      case evalList (c, {l, r}) of
        [c1, {vv, v}] -> [c1, evalOp (op, vv, v)]
      esac
  | Skip -> [c, BOTTOM]
  | Assn (l, r) -> 
      case evalList (c, {l, r}) of
        [[s1, w1], {Ref (vv), v}] -> [[s1 <- [vv, Val (v)], w1], v]
      | [c1, {ElemRef (a, i), v}] -> a[i]:=v; [c1, v]
      esac
  | Seq (s1, s2) -> 
      case eval (c, s1) of
        [c1, _] -> eval (c1, s2)
      esac
  | If (e, s1, s2) -> 
      case eval (c, e) of
        [c1, 0] -> eval (c1, s2)
      | [c1, _] -> eval (c1, s1)
      esac
  | While (e, s1) -> 
      case eval (c, e) of
        [c1, 0] -> [c1, BOTTOM]
      | [c1, _] -> case eval (c1, s1) of [c2, _] -> eval (c2, expr) esac
      esac
  | Repeat (s1, e) -> 
      case eval (c, s1) of
        [c1, _] -> 
          case eval (c1, e) of
            [c2, 0] -> eval (c2, expr)
          | [c2, _] -> [c2, BOTTOM]
          esac
      esac
  | Ignore (s) -> 
      case eval (c, s) of 
        [c1, _] ->
          [c1, BOTTOM]
      esac 
  | Scope (defs, e) -> 
      case eval ([evalDefs (enterScope (s), defs), w], e) of
        [[s1, w1], vv] -> [[leaveScope (s1), w1], vv]
      esac
  | Call (f, args) -> 
      case lookupFun (s, f) of
        Fun (as, e) ->
          case evalList (c, args) of
            [[s1, w1], vs] -> 
                case e of
                  External -> 
                    case evalBuiltin (f, vs, w1) of
                      [z, wd] -> [[s1, wd], z]
                    esac
                | _ -> 
                    case eval ([addNamedValues (enterFunction (s1), as, vs), w1], e) of
                      [[s2, w2], vv] -> [[leaveFunction (s1, getGlobal (s2)), w2], vv]
                    esac
                esac
          esac                
      esac
  | String (str) -> [c, str]
  | Array (args) -> 
      case evalList (c, args) of
        [c1, vs] -> [c1, listArray (vs)]
      esac
  | Elem (e, i) -> 
      case evalList (c, {e, i}) of
        [c1, {l, v}] -> [c1, l[v]]
      esac
  | ElemRef (e, i) -> 
      case evalList (c, {e, i}) of
        [c1, {l, v}] -> 
          [c1, ElemRef (l, v)]
      esac
  | Builtin (f, args) -> 
      case evalList (c, args) of
        [[s1, w1], vs] -> 
          case evalBuiltin (f, vs, w1) of
            [z, wd] -> [[s1, wd], z]
          esac
      esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read", Fun ({}, External)).addName ("write", Fun (singleton ("a"), External)) , createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}