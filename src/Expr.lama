-- Expression evaluator

import List;
import State;

local ops = {
  ["+", infix +],
  ["-", infix -],
  ["*", infix *],
  ["/", infix /],
  ["%", infix %],
  ["==", infix ==],
  ["!=", infix !=],
  ["<", infix <],
  ["<=", infix <=],
  [">", infix >],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

public fun evalBinop(op, left, right){
  case assoc (ops, op) of
    Some (f) -> f(left, right)
  esac
}

-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun evalExpr (st, expr) {
  case expr of
   Const (x) -> x
   | Var (x) -> st (x)
   | Binop (o, l, r) -> evalBinop (o, evalExpr (st, l), evalExpr (st, r) )
  esac
}
