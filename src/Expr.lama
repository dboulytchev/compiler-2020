-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
public fun evalExpr (st, expr) {
  case expr of
      Const (n) -> n
    | Var (x) -> st(x)
    | Binop (op, l, r) -> op.evalBinop(st.evalExpr(l), st.evalExpr(r))
  esac
}

-- The evaluator for binary operators
public fun evalBinop (op, l, r) {
  case op of
      "+" -> l + r
    | "-" -> l - r
    | "*" -> l * r
    | "/" -> l / r
    | "%" -> l % r
    | "==" -> l == r
    | "!=" -> l != r
    | "<" -> l < r
    | "<=" -> l <= r
    | ">" -> l > r
    | ">=" -> l >= r
    | "&&" -> l && r
    | "!!" -> l !! r
  esac
}