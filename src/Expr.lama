-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun evalExpr (st, expr) {
    case expr of
        Const(n) -> n
      | Var(v)   -> st(v)
      | Binop(oprStr, l, r) -> evalBinop(oprStr)(evalExpr(st, l), evalExpr(st, r))
      | _      -> failure ("Expresion is not implemented\n")
    esac
}

public fun evalBinop(oprStr) {
  case oprStr of
    "+"  -> infix +
  | "-"  -> infix -
  | "*"  -> infix *
  | "/"  -> infix /
  | "%"  -> infix %
  | "==" -> infix ==
  | "!=" -> infix !=
  | "<"  -> infix <
  | "<=" -> infix <=
  | ">"  -> infix >
  | ">=" -> infix >=
  | "&&" -> infix &&
  | "!!" -> infix !!
  esac
}