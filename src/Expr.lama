-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun evalExpr (state, expr) {
  case expr of
      Const (n)        -> n
    | Var   (x)        -> state(x)
    | Binop (op, l, r) -> op.evalBinop(state.evalExpr(l), state.evalExpr(r))
  esac
}

-- The evaluator for binary operators
public fun evalBinop (op, l, r) {
  case op of
      "+"  -> l + r
    | "-"  -> l - r
    | "*"  -> l * r
    | "/"  -> l / r
    | "%"  -> l % r
    | "==" -> l == r
    | "!=" -> l != r
    | "<"  -> l < r
    | "<=" -> l <= r
    | ">"  -> l > r
    | ">=" -> l >= r
    | "&&" -> l && r
    | "!!" -> l !! r
    | _    -> failure("Unknown operator: %s\n", op)
  esac
}
