-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
local ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        Repeat  (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, val] -> [c, val : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun evalDefs(s, defs) {
  foldl (fun (s, def) {
              case def of
                Local (xs) -> addNames(s, xs)
              | Fun (f, args, e) -> addFunction(s, f, args, e)  
              esac
            }, s, defs
        )
}

fun addNamesValues (state, names, vals) {
  foldl (fun (s, [name, val]) {
           s.addName (name, Val (val))
         }, state, zip(names, vals))
}

fun chain(cfg, expr1, expr2) {
    case eval(cfg, expr1) of [cNew, _] -> eval(cNew, expr2) esac
}

fun eval (c@[s, w],expr) { case expr of
    Const(value) -> [c, value]
  | Var(name) -> [c, case lookupVal(s, name) of Val(value) -> value esac]
  | Ref(name) -> lookupVal(s, name); [c, Ref(name)]
  | Binop(op, left, right) -> case evalList(c, {left, right}) of
      [cNext, {leftResult, rightResult}] -> [cNext, evalOp(op, leftResult, rightResult)]
  esac
  | Skip -> [c,BOTTOM]
  | Assn(left, right) -> case evalList(c, {left, right}) of
      [[sNext, wNext], {Ref(name), value}] -> [[sNext <- [name, Val(value)], wNext], value]
    | [c1, {ElemRef (a, i), value}] -> a[i] := value; [c1, value]
  esac
  | Seq(stmt1, stmt2) -> case eval(c, stmt1) of
      [cNew, _] -> eval(cNew, stmt2)
  esac
  | If(cond, onTrue, onFalse) -> case eval(c, cond) of
      [cNew, value] -> eval(cNew, if value == 0 then onFalse else onTrue fi)
  esac
  | While(cond, body) -> case eval(c, cond) of
      [cNew, value] -> if value == 0 then [cNew,BOTTOM] else chain(cNew, body, expr) fi
  esac
  | Repeat(body, cond) -> case eval(c, body) of
      [cBody, _] -> case eval (cBody, cond) of
          [cCond, value] -> if value == 0 then eval(cCond, expr) else [cCond, BOTTOM] fi
      esac
  esac

  | Ignore(stmt) -> case eval(c, stmt) of
      [cNew, _] -> [cNew, BOTTOM]
  esac
  | Scope(defs, expr) -> case eval([evalDefs(enterScope(s), defs), w], expr) of
      [[sNew, wNew], result] -> [[leaveScope(sNew), wNew], result]
  esac
(*
  | Call(name, args) -> case lookupFun(s, name) of
      Fun(sig, body) -> case evalList(c, args) of
          [[sArgs, wArgs], resultArgs] -> case eval([amendState(enterFunction(sArgs), sig, resultArgs), wArgs], body) of
              [[sBody, wBody], resultBody] -> [[leaveFunction(sArgs, getGlobal(sBody)), wBody], resultBody]
          esac
      esac
    esac
  esac
*)

(*  | Ignore (s) -> case eval(c, s) of [c1, _] -> [c1, BOTTOM] esac 
  | Scope (defs, e) -> case eval([evalDefs(enterScope(s), defs), w], e) of
                         [[s1, w1], vv] -> [[leaveScope(s1), w1], vv]
                       esac
*)
  | Call (f, args) -> case lookupFun(s, f) of
                        Fun (as, e) -> case evalList(c, args) of
                                         [[s1, w1], vs] -> case e of
                                                             External -> case evalBuiltin(f, vs, w1) of
                                                                           [z, wd] -> [[s1, wd], z]
                                                                         esac
                                                           | _        -> case eval ([addNamesValues(enterFunction(s1), as, vs), w1], e) of
                                                                           [[s2, w2], vv] -> [[leaveFunction(s1, getGlobal(s2)), w2], vv]
                                                                         esac
                                                           esac
                                       esac                
                      esac

  | String(value) -> [c, value]

  | Array(elems) -> case evalList(c, elems) of
    [cNew, values] -> [cNew, listArray(values)]
  esac

  | Elem(e, i) -> case evalList(c, {e, i}) of
                     [c1, {l, v}] -> [c1, l[v]]
                   esac
  | ElemRef (e, i) -> case evalList(c, {e, i}) of
                        [c1, {l, v}] -> [c1, ElemRef (l, v)]
                      esac
  | Builtin (f, args) -> case evalList(c, args) of
                           [[s1, w1], vs] -> case evalBuiltin(f, vs, w1) of
                                               [z, wd] -> [[s1, wd], z]
                                             esac
                         esac
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read", Fun ({}, External)).addName ("write", Fun (singleton ("a"), External)) , createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
