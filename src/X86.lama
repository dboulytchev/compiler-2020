-- X86 codegeneration interface
-- We use stack machine programs as intermediate representation
import Fun;
import SM;
import Collection;
import List;
import Util;
import Manifest;

-- Assembler language interface
-- The registers: 
local regs = ["%ebx", "%ecx", "%esi", "%edi", "%eax", "%edx", "%ebp", "%esp"];

-- We can not freely operate with all register; only with 4 by now
local nRegs = regs.length - 5;

-- For convenience we define the following synonyms for the registers: 
local ebx = R (0),
      ecx = R (1),
      esi = R (2),
      edi = R (3),
      eax = R (4),
      edx = R (5),
      ebp = R (6),
      esp = R (7);

-- We need to know the word size to calculate offsets correctly
local wordSize = 4;

-- We need to distinguish the following operand types: 
--    R (int)       -- hard register                    
--    S (int)       -- a position on the hardware stack 
--    M (string)    -- a named memory location          
--    L (int)       -- an immediate operand
--    I (int, opnd) -- an indirect operand with offset

-- Some x86 instruction (we do not need all of them):
--   Mov   (opnd, opnd)         -- copies a value from the first to the second operand  
--   Lea   (opnd, opnd)         -- loads an address of the first operand into the second
--   Binop (string, opnd, opnd) -- makes a binary operation; note, the first operand   
--                                 designates x86 operator, not the source language one
--   IDiv  (opnd)               -- x86 integer division, see instruction set reference 
--   Cltd                       -- see instruction set reference           
--   Set   (string, string)     -- sets a value from flags; the first operand is the   
--                                 suffix, which determines the value being set, the
--                                 the second --- (sub)register name
--   Jmp   (string)             -- unconditional jump to a label
--   CJmp  (string, string)     -- conditional jump to a label
--   Label (string)             -- a label
--   Push  (opnd)               -- pushes the operand on the hardware stack
--   Pop   (opnd)               -- pops from the hardware stack to the operand
--   Call  (string)             -- calls a function by its name 
--   Ret                        -- returns from a function
--   Meta  (string)             -- metainformation (declarations, etc.)
                                                               
-- Machine instruction printer
fun insnString (insn) {
  
  fun binopString (op) {
    case op of
      "+"   -> "addl"
    | "-"   -> "subl"
    | "*"   -> "imull"
    | "&&"  -> "andl"
    | "!!"  -> "orl" 
    | "^"   -> "xorl"
    | "cmp" -> "cmpl"
    esac
  }

  fun opndString (opnd) {
    case opnd of
      R (i)    -> regs [i]
    | S (i)    ->  if i >= 0
                   then sprintf ("-%d(%%ebp)", (i+1) * wordSize)
                   else sprintf ("%d(%%ebp)", 8 + (-1-i) * wordSize)
                   fi

    | M (x)    -> x
    | L (i)    -> sprintf ("$%d", i)
    | I (0, x) -> sprintf ("(%s)", opndString (x))
    | I (n, x) -> sprintf ("%d(%s)", n, opndString (x))
    esac
  }

  case insn of
    Cltd               -> "\tcltd\n"
  | Set   (suf, s)     -> sprintf ("\tset%s\t%s\n", suf, s)
  | IDiv  (s1)         -> sprintf ("\tidivl\t%s\n", opndString (s1))
  | Binop (op, s1, s2) -> sprintf ("\t%s\t%s,\t%s\n", binopString (op), opndString (s1), opndString (s2))
  | Lea   (s1, s2)     -> sprintf ("\tleal\t%s,\t%s\n", opndString (s1), opndString (s2))
  | Mov   (s1, s2)     -> sprintf ("\tmovl\t%s,\t%s\n", opndString (s1), opndString (s2))
  | Push  (s)          -> sprintf ("\tpushl\t%s\n", opndString (s))
  | Pop   (s)          -> sprintf ("\tpopl\t%s\n", opndString (s))
  | Ret                -> "\tret\n"
  | Call  (p)          -> sprintf ("\tcall\t%s\n", p)
  | Label (l)          -> sprintf ("%s:\n", l)
  | Jmp   (l)          -> sprintf ("\tjmp\t%s\n", l)
  | CJmp  (c, l)       -> sprintf ("\tj%s\t%s\n", c, l)
  | Meta  (m)          -> m
  esac
}

-- Environment for symbolic interpreter
-- An environment holds
--    a symbolic stack
--    a maximal stack depth reached so far
--    a set of global variable names
--    a barrier condition (set up right after an unconditional jump is interpreter)
--    a stack map which maps labels to symbolic stacks

fun makeEnv (stack, stackSlots, globals, barrier, stackMap, fLabel) {
  -- Returns an internal name for a global variable
  fun globalName (name) {
    "global_" ++ name
  }

  -- Returns a string representation of the environment
  fun envString () {
    sprintf ("Stack     : %s\nStackSlots: %d\nGlobals   : %s\n", stack.string, stackSlots, elements (globals).string)
  }
  
  -- Allocates a new position on the symbolic stack;
  -- returns a pair: a location for allocated item and
  -- an updated environment
  fun allocate () {
    case 
      case stack of
        {}        -> [ebx, 0]
      | S (n) : _ -> [S (n+1), n+2]
      | R (n) : _ -> if n < nRegs then [R (n+1), stackSlots] else [S (0), 1] fi
      | _         -> [S (0), 1]
      esac
    of [x, n] -> [x, makeEnv (x : stack, if n > stackSlots then n else stackSlots fi, globals, barrier, stackMap, fLabel)]esac
  }

  -- Pushes an item on the symbolic state; returns an updated envirtonment
  fun push (y) {
    makeEnv (y : stack, stackSlots, globals, barrier, stackMap, fLabel)
  }

  -- Pops one item from the symbolic stack; returns a pair: a popped
  -- item and an updated environment
  fun pop () {
    case stack of
      x : stack -> [x, makeEnv (stack, stackSlots, globals, barrier, stackMap, fLabel)]
    esac
  }

  -- Pops two items from the symbolic stack; returns a triple:
  -- popped items and an undated environment
  fun pop2 () {
    case stack of
      x : y : stack -> [x, y, makeEnv (stack, stackSlots, globals, barrier, stackMap, fLabel)]
    esac
  }

  -- Peeks the top of the symbolic stack
  fun peek () {
    stack.fst
  }
  
  -- Adds a global variable; returns an updated environment
  fun addGlobal (name) {
    makeEnv (stack, stackSlots, addSet (globals, globalName (name)), barrier, stackMap, fLabel)
  }

  -- References an allocation
  fun loc (alloc) {
    case alloc of
      Glb (x) -> M (globalName (x))
    | Arg (i) -> S (-1-i)
    | Loc (i) -> S (i)
    esac
  }
  
  -- Gets a list of global variables from the environment
  fun getGlobals () {
    globals.elements
  }

  -- Gets a maximal stack size from the environment
  fun getStackSize () {
    stackSlots
  }

  -- Gets barrier condition
  fun isBarrier () {
    barrier
  }

  -- Sets barrier condition
  fun setBarrier () {
    makeEnv (stack, stackSlots, globals, true, stackMap, fLabel)
  }

  -- Associates the current symbolic state with a label
  fun setStack (l) {
    makeEnv (stack, stackSlots, globals, false, addMap (stackMap, l, stack), fLabel)
  }

  -- Retrieves a stack for a label
  fun retrieveStack (l) {
    case findMap (stackMap, l) of
      None         -> makeEnv (stack, stackSlots, globals, false, stackMap, fLabel)
    | Some (stack) -> makeEnv (stack, stackSlots, globals, false, stackMap, fLabel)
    esac
  }

  -- Sets the label of current function
  fun enterFunction (fLabel) {
    makeEnv (stack, stackSlots, globals, false, stackMap, fLabel)
  }

  -- Gets the label of current function
  fun currentFunction () {
    fLabel
  }

  -- Gets the list of live registers past given symbolic stack depth
  fun liveRegisters (depth) {
    fun inner (d, acc, st) {
      case st of
        {}             -> acc
      | (r@R (_)) : tl -> inner (d+1, if d >= depth then r : acc else acc fi, tl)
      | _         : tl -> inner (d+1, acc, tl)
      esac
    }

    inner (0, {}, stack)
  }
  
  [envString,
   allocate,
   push,
   pop,
   pop2,
   addGlobal,
   loc,
   getGlobals,
   getStackSize,
   peek,
   isBarrier,
   setBarrier,
   setStack,
   retrieveStack,
   enterFunction,
   currentFunction,
   liveRegisters]
}

-- Exported accessors
fun envString (env) {
  env [0] ()
}

fun allocate (env) {
  env [1] ()
}

fun push (env, x) {
  env [2] (x)
}

fun pop (env) {
  env [3] ()
}

fun pop2 (env) {
  env [4] ()
}

fun addGlobal (env, name) {
  env [5] (name)
}

fun loc (env, name) {
  env [6] (name)
}

fun getGlobals (env) {
  env [7] ()
}

fun getStackSize (env) {
  env [8] ()
}

fun peek (env) {
  env [9] ()
}

fun isBarrier (env) {
  env [10] ()
}

fun setBarrier (env) {
  env [11] ()
}

fun setStack (env, l) {
  env [12] (l)
}

fun retrieveStack (env, l) {
  env [13] (l)
}

fun enterFunction (env, fLabel) {
  env [14] (fLabel)
}

fun currentFunction (env) {
  env [15] ()
}

fun liveRegisters (env, depth) {
  env [16] (depth)
}

-- Creates an initial environment
fun initEnv () {
  makeEnv (0, emptySet (), {}, false, emptyMap (), "")
}

-- Codegeneration helper functions
-- Generates code section
fun codeSection (text) {
  Meta ("\t.text\n") : text
}

-- Generates data section
fun dataSection (text) {
  Meta ("\t.data\n") : text
}

-- Generates data definition
fun dataDef (name) {
  singleton (Meta (sprintf ("%s:\t.int\t0\n", name)))
}

-- Generates function prologue
fun prologue (size) {
  {Push  (ebp),
   Mov   (esp, ebp),
   if size > 0
   then Binop ("-", L (wordSize*size), esp)
   else {}
   fi
  }
}

-- Generates function epilogue
fun epilogue (env) {
  if compare (env.currentFunction, "main") == 0
  then [env, {Mov (ebp, esp), Pop (ebp), Binop ("^", eax, eax), Ret}]
  else case env.pop of
         [y, env] -> [env, {Mov (ebp, esp), Pop (ebp), Mov (y, eax), Ret}]
       esac
  fi
}

-- Checks if an operand resides on a stack
fun stackOpnd (opnd) {
  case opnd of
    S (_) -> true
  | _     -> false
  esac      
}

-- Checks if an operand resides in memory
fun memOpnd (opnd) {
  case opnd of
    S (_) -> true
  | M (_) -> true
  | _     -> false
  esac
}

-- Generates a move between locations, using
-- intermediate register if needed
fun move (from, to) {
  if memOpnd (from) && memOpnd (to)
  then {Mov (from, eax), Mov (eax, to)}
  else singleton (Mov (from, to))
  fi
}


-- Gets a suffix for Set instruction from
-- source language comparison operator
fun suffix (op) {
  case op of
    "<"  -> "l"
  | "<=" -> "le"
  | "==" -> "e"
  | "!=" -> "ne"
  | ">=" -> "ge"
  | ">"  -> "g"
  esac
}

-- Compiles stack machine code into a list of x86 instructions. Takes an environment
-- and stack machine code, returns an updated environment and x86 code.
fun compile (env, code) {
  fun compile (env, code) {
    fun compileOp (lhs, rhs, s, op) {
      case op of
        "+" -> simpleOp(lhs, rhs, s, op)
      | "-" -> simpleOp(lhs, rhs, s, op)
      | "*" -> simpleOp(lhs, rhs, s, op)
      | "/" -> divOp(lhs, rhs, s, op)
      | "%" -> divOp(lhs, rhs, s, op)
      | "<" -> cmpOp(lhs, rhs, s, op)
      | ">" -> cmpOp(lhs, rhs, s, op)
      | "<=" -> cmpOp(lhs, rhs, s, op)
      | ">=" -> cmpOp(lhs, rhs, s, op)
      | "==" -> cmpOp(lhs, rhs, s, op)
      | "!=" -> cmpOp(lhs, rhs, s, op)
      | "&&" -> logOp(lhs, rhs, s, op)
      | "!!" -> logOp(lhs, rhs, s, op)
      esac
    }
    fun simpleOp (lhs, rhs, s, op) {
      { Mov (rhs, eax),  Binop (op, lhs, eax), Mov (eax, s)}
    }
    fun divOp (lhs, rhs, s, op) {
      { Mov (rhs, eax), Cltd, IDiv(lhs),
        case op of "/" -> Mov(eax, s)
                  |"%" -> Mov(edx, s)
        esac
      }
    }
    fun cmpOp (lhs, rhs, s, op) {
      { Mov (rhs, edx), Binop ("^", eax, eax), Binop("cmp", lhs, edx), Set (suffix(op), "%al"), Mov (eax, s)}
    }
    fun logOp (lhs, rhs, s, op) {
      { Binop("^", eax, eax), Binop("^", edx, edx), Binop("cmp", lhs, eax), Set(suffix("!="), "%al"),
        Binop("cmp", rhs, edx), Set(suffix("!="), "%dl"), Binop(op, eax, edx), Mov(edx, s)}
    }
    fun pushArgs (env, xcode, n) {
      case n of
          0 -> [env, reverse(xcode)]
        | _ -> case env.pop of  [x, env] -> pushArgs (env, Push (x) : xcode, n - 1) esac
      esac
    }
    foldl (
      fun ([env, scode], i) {
        local code = scode : singleton (Meta ("# " ++ showSMInsn (i) ++ "\n"));
        case i of
          READ ->
            case env.allocate of
              [s, env] -> [env, code : {Call ("Lread"), Mov (eax, s)}]
            esac             
        | WRITE ->
            case env.pop of
              [s, env] -> [env, code : {Push (s), Call ("Lwrite"), Pop (eax)}]
            esac
        
         -- Some guidelines for generating function calls:
         --
         -- 1. generate instructions to save live registers on the X86 stack (use
         --    env.liveRegisters (number of arguments);
         -- 2. generate instructions to move actual parameters from the symbolic
         --    stack to the hardware one;
         -- 3. generate the call itself;
         -- 4. discard the actual parameters from the stack;
         -- 5. restore saved live registers.
         --
         -- Some guidelines for generating functions:
         --
         -- 1. generate proper prologue for BEGIN instruction (use "prologue" helper); use
         --    env.enterFunction to create a proper environment;
         -- 2. generate epilogue for END instruction.
        | CONST (n) ->
            case env.allocate of 
              [s, env] -> [env, code : singleton(Mov (L(n), s))]
            esac
        | BINOP (op) ->
            case env.pop2 of
              [lhs, rhs, env] ->
                  case env.allocate of 
                    [s, env] -> [env, code : compileOp(lhs, rhs, s, op)]
                  esac
            esac
        | LABEL (l) -> [if env.isBarrier then env.retrieveStack (l) else env fi, code : singleton (Label (l))]
        | JMP (l) -> [setBarrier (setStack (env, l)), code : singleton (Jmp (l))]
        | CJMP (x, l) -> 
            case env.pop of 
              [s, env] -> [setStack (env, l), code : { Binop ("cmp", L (0), s), CJmp(x, l) }]
            esac        
        | ST (x) -> [env, code : move(env.peek, env.loc(x))]
        | LD (x) ->
            case env.allocate of
              [s, env] -> [env, code : move(env.loc(x), s)]
            esac
        | LDA (x) ->
            case env.allocate of
              [s, env] -> [env, code : {Lea (env.loc(x), eax), Mov (eax, s)}]
            esac
        | STI ->
            case env.pop2 of
              [v, x, env] -> [env.push(x), code : {Mov (v, eax), Mov (x, edx), Mov (eax, I (0, edx)), Mov (eax, x)}]
            esac
        | DROP -> case env.pop of [_, env] -> [env, code] esac
        | CALL (fLabel, nArgs) -> 
              local pushR = map (fun (r) {Push (r)}, env.liveRegisters (nArgs));
              local popR = map (fun (r) {Pop (r)}, env.liveRegisters (nArgs));
              case pushArgs (env, {}, nArgs) of 
                [env, args] -> 
                  case env.allocate of
                    [s, env] -> [env, code : {pushR, args, Call (fLabel), 
                                              Binop ("+", L (wordSize * args.size), esp),
                                              reverse (popR), Mov (eax, s)}]
                  esac
              esac
        | GLOBAL (x) -> [env.addGlobal (x), code]
        | BEGIN (f, a, n) -> [env.enterFunction (f), {code, prologue (n)}]
        | END -> case epilogue (env) of [env, eCode] -> [env, code : eCode] esac
        | _ -> failure ("codegeneration for instruction %s is not yet implemented\n", i.string)
        esac
      }, [env, {}], code)
  }

  compile (env, code) 
}

-- A top-level codegeneration function. Takes a driver's environment and a stack machine program,
-- compiles the program into machine code, and compiles the machine code into an executable
public fun compileX86 (args, code) {
  case compile (initEnv (), code) of
    [env, code] ->
       local asmFile = args.getBaseName ++ ".s",
             runtime = case getEnv ("LAMA_RUNTIME") of
                         #unboxed -> "../runtime/"
                       | path     -> path
                       esac ++ "/runtime.o";
       
       fwrite (asmFile,
               map (insnString, 
                    deepFlatten ({
                      Meta ("\t.global\tmain\n"),
                      dataSection (
                        map (dataDef, getGlobals (env))
                      ),
                      codeSection (code)
                    })).stringcat);
                    
       system ({"gcc -g -m32 -o ", args.getBaseName, " ", runtime, " ", asmFile}.stringcat)
  esac
}
