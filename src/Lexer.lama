-- Lexical analysis using Matcher

import Matcher;
import Ostap;
import Fun;
import Collection;
import List;

-- A hashtable to keep a source code location information.
-- The keys in this table is a (boxed) value, and values are
-- pairs [line, column]
local srcTab = emptyHashTab ();

-- A custom combinator to additionally add location info
public fun located (p) {
  loc |> fun (l) {p @ fun (x) {srcTab := addHashTab (srcTab, x, l); x}}
}

-- Retrieves location info; fail if no info found
public fun getLoc (s) {
  case findHashTab (srcTab, s) of Some (loc) -> loc esac
}

-- A set of all keywords
local keywords =
   foldl (addSet,
          emptySet (),
          {"read",
           "write",
           "skip",
           "if",
           "then",
           "else",
           "elif",
           "fi",
           "while",
           "do",
           "od",
           "for",
           "repeat",
           "until"});

-- Regular expression definitions
public rWhiteSpace  = createRegexp ("\\(\\([ \n\r\t]*\\)\\|\\(--[^\n]*[ \n\r\t]*\\)\\)*", "whitespace"),
       rDecimal     = createRegexp ("-?[0-9]+", "decimal constant"),
       rLident      = createRegexp ("[a-z][a-z_A-Z0-9]*", "lowercase identifier"),
       rRead        = createRegexp ("read\\b", """read"""),
       rWrite       = createRegexp ("write\\b", """write"""),
       rSkip        = createRegexp ("skip\\b", """skip"""),
       rIf          = createRegexp ("if\\b", """if"""),
       rThen        = createRegexp ("then\\b", """then"""),
       rElse        = createRegexp ("else\\b", """else"""),
       rElif        = createRegexp ("elif\\b", """elif"""),
       rFi          = createRegexp ("fi\\b", """fi"""),
       rWhile       = createRegexp ("while\\b", """while"""),
       rDo          = createRegexp ("do\\b", """do"""),
       rOd          = createRegexp ("od\\b", """od"""),
       rRepeat      = createRegexp ("repeat\\b", """repeat"""),
       rUntil       = createRegexp ("until\\b", """until"""),
       rFor         = createRegexp ("for\\b", """for""");
      
local whiteSpace = token (rWhiteSpace);

-- Public parsers for concrete tokens
-- Generic token parser; t is either a string or a regular expression
public fun s (t) {memo $ whiteSpace |> lift (token (t))}

-- Some individual tokens
public kRead    = s ("read"),
       kWrite   = s ("write"),
       kSkip    = s ("skip"),
       kIf      = s ("if"),
       kThen    = s ("then"),
       kElse    = s ("else"),
       kElif    = s ("elif"),
       kFi      = s ("fi"),
       kWhile   = s ("while"),
       kDo      = s ("do"),
       kOd      = s ("od"),
       kRepeat  = s ("repeat"),
       kUntil   = s ("until"),
       kFor     = s ("for"),
       decimal  = s (rDecimal),
       pos      = memo $ whiteSpace |> lift (loc),
       end      = memo $ whiteSpace |> lift (eof),
       lident   = memo $       
         whiteSpace |> lift (fun (k) {
                                fun (s) {
                                  k (case s.matchRegexp (rLident) of
                                       a@Succ (v, _) -> if memSet (keywords, v)
                                                        then Fail ("lowercase identifier expected", s.getLine, s.getCol)
                                                        else a
                                                        fi
                                     | a             -> a
                                     esac)
                                }
                              });

