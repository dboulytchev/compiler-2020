-- Stack machine.

import List;
import World;
import State;
import Expr;
import Util;

-- Stack machine interpreter. Takes an SM-configuration and a program,
-- returns a final configuration
fun eval (conf, insns) {
  foldl(fun (c@[stack, state, world], instr) {
    case [stack, instr] of
      [x : y : s, Binop(op)] -> [evalBinop(op)(y, x) : s, state, world]
    | [s        , Const(n)     ] -> [n : s, state, world]
    | [s        , Read         ] -> case readWorld(world) of 
                                      [n, w] -> [n : s, state, w] 
                                    esac
    | [z : s    , Write        ] -> [s, state, writeWorld(z, world)]
    | [s        , LD(var)      ] -> [state(var) : s, state, world]
    | [z : s    , ST(var)      ] -> [s, state <- [var, z], world]
    esac
  }, conf, insns)
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval ([{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compiles an expression into a stack machine code.
-- Takes an expression and a list where to put the resulting
-- stack machine instructions
-- @type [Expr, {SMInstr}] -> {SMInstr}
fun compileExpr (expr, acc) {
  case expr of
    Const(n) -> Const(n) : acc
  | Var(x)   -> LD(x) : acc
  | Binop(opr, l, r) -> Binop(opr) : compileExpr(r, compileExpr(l, acc))
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
-- @type Stmt -> {SMInstr}
public fun compileSM (stmt) {
  fun compileSM (stmt, acc) {
    case stmt of
      Skip -> acc
    | Assn(varStr, expr) -> ST(varStr) : compileExpr(expr, acc)
    | Read(varStr) -> ST(varStr) : Read : acc
    | Write(expr)  -> Write : compileExpr(expr, acc)
    | Seq(s1, s2)  -> compileSM(s2, compileSM(s1, acc))
    esac
  }
  reverse(compileSM(stmt, {}))
}
