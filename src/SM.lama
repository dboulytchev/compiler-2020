-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Util;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  local map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, conf @ [stack, state, world], insns) {
	case insns of
		  CONST (c) : ins -> eval (env, [c : stack, state, world], ins)	
		| READ : ins ->
        case readWorld (world) of
          [v, nw] -> 
            eval (env, [v : stack, state, nw], ins)
        esac
		| WRITE : ins ->
        case stack of
          head : t -> 
            local nw = writeWorld (head, world);
            eval (env, [t, state, nw], ins)
        esac	
		| ST (x) : ins -> 
        case stack of
          head : t -> 
            eval (env, [t, state <- [x, head], world], ins)
        esac
		| LD (x) : ins -> eval (env, [state (x) : stack, state, world], ins)	
		| BINOP (o) : ins -> 
        case stack of
          r : l : t -> 
            local res = evalBinop (o, l, r);
            eval (env, [res : t, state, world], ins)
        esac
		| LABEL (_) : instr -> eval (env, conf, instr)
    | JMP (lb) : instr -> eval (env, conf, fromLabel (env, lb))
    | CJMP ("z", lb) : instr ->
        case stack of 
          0 : rest -> eval (env, [rest, state, world], fromLabel (env, lb))
          |
          _ : rest -> eval (env, [rest, state, world], instr)
        esac
    | CJMP ("nz", lb) : instr ->
        case stack of 
          0 : rest -> eval (env, [rest, state, world], instr)
          |
          _ : rest -> eval (env, [rest, state, world], fromLabel (env, lb))
        esac
		| {} -> [stack, state, world]
	esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then singleton (env)
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list (of, possibly, lists)
-- of stack machine instructions
fun compileExpr (expr) {
  case expr of
      Const (c)        -> singleton(CONST (c))
    | Var   (x)        -> singleton(LD (x))
    | Binop (op, l, r) -> { compileExpr(l), compileExpr(r), BINOP (op) }
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  fun compile_ (stmt, env) {
      case stmt of
          Skip -> [{}, env]
        | Read (x) -> [{READ, ST (x)}, env]
        | Write (e) -> [{compileExpr (e), WRITE}, env]
        | Assn (x, e) -> [{compileExpr (e), ST (x)}, env]
        | Seq (s1, s2) ->
            case compile_ (s1, env) of [instr1, env1] ->
              case compile_ (s2, env1) of [instr2, env2] ->
                [{instr1, instr2}, env2]
              esac
            esac
        | While (e, body) ->
            case genLabels (env,2) of [lBody, lCheck, envL] ->
              case compile_ (body, envL) of [instr1, env1] ->
                [{JMP (lCheck), LABEL (lBody), instr1, LABEL (lCheck), compileExpr (e), CJMP ("nz", lBody)}, env1]
              esac
            esac
        | If (e, sIf, sElse) -> 
            case genLabels (env, 2) of [lElse, lFi, envL] ->
              case compile_ (sIf, envL) of [instr1, env1] ->
                case compile_ (sElse, env1) of [instr2, env2] ->
                  [{compileExpr (e), CJMP ("z", lElse), instr1, JMP (lFi), LABEL (lElse), instr2, LABEL (lFi)}, env2]
                esac
              esac
            esac
        | Repeat (body, e) ->
            case genLabels (env, 1) of [lBody, envL] ->
              case compile_ (body, envL) of [instr1, env1] ->
                [{LABEL (lBody), instr1, compileExpr (e), CJMP ("z", lBody)}, env1]
              esac
            esac
      esac
  }

  compile_ (stmt, initCompEnv ()).fst.deepFlatten
}
