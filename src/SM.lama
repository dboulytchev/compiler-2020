-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Util;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> "READ"
  | WRITE        -> "WRITE"
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | LDA   (x)    -> sprintf ("LDA %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | STI          -> "STI"
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  | DUP          -> "DUP"
  | DROP         -> "DROP"  
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  local map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c@[stack, state, world], insns) {
    case insns of
          CONST(n) : insns -> eval(env, [n:stack, state, world], insns)
        | BINOP(op) : insns ->  case stack of
            x : y : stack -> eval(env, [evalOp(op, y, x) : stack, state, world], insns)
          esac
        | READ : insns ->  case readWorld(world) of
            [n, newWorld] -> eval(env, [n : stack, state, newWorld], insns)
          esac
        | WRITE : insns -> case stack of
            n : stack -> eval(env, [stack, state, writeWorld(n, world)], insns)
          esac
        | LD(x) : insns ->  eval(env, [state(x) : stack, state, world], insns)
        | LDA (x) : insns ->  eval(env, [Ref (x) : stack, state, world], insns)
        | ST(x) : insns ->  case stack of
            n : _ -> eval(env, [stack, state <- [x, n], world], insns)
          esac
        | STI : insns ->  case stack of
            n : Ref(x) : stack -> eval(env, [n:stack, state <- [x, n], world], insns)
          esac
        | LABEL(l) : insns -> eval(env, c, insns)
        | JMP(l) : insns -> eval(env, c, fromLabel(env, l))
        | CJMP("nz", l) : insns -> case stack of
            0 : stack -> eval(env, [stack, state, world], insns)
            | _ : stack -> eval(env, [stack, state, world], fromLabel(env, l))
          esac
        | CJMP("z", l) : insns -> case stack of
            0 : stack -> eval(env, [stack, state, world], fromLabel(env, l))
            | _ : stack -> eval(env, [stack, state, world], insns)
          esac
        | DROP : insns -> case stack of
            _ : stack -> eval(env, [stack, state, world], insns)
          esac
        | _ -> c
    esac
}


-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then singleton (env)
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  fun compile (stmt, env) {
      case stmt of
          Const(val) -> [singleton(CONST(val)), env]
        | Var(var) -> [singleton(LD (var)), env]
        | Ref(ref) -> [singleton(LDA (ref)), env]
        | Read(x) -> [{READ, ST (x), DROP}, env]
        | Write(expr) -> case compile(expr, env) of [ce, env1] ->
                          [{ce, WRITE}, env1]
                       esac
        | Assn(var, expr) -> case compile(var, env) of [cs1, env1] ->
                          case compile(expr, env1) of [cs2, env2] ->
                               [{cs1, cs2, STI}, env2]
                          esac
                         esac
        | Seq (a, b) -> 
              case compile(a, env) of [cs1, env1] -> 
                case compile(b, env1) of [cs2, env2] -> 
                  [{cs1, cs2}, env2]
                esac
              esac
        | Skip -> [{}, env]
        | If(x, a, b) -> 
             case genLabels(env, 2) of [ls2, lexit, env1] ->
                case compile(a, env1) of [cs1, env2] ->
                  case compile(b, env2) of [cs2, env3] ->
                    case compile(x, env3) of [ce, env3] ->
                      [{ce, CJMP("z", ls2), cs1, JMP(lexit), LABEL(ls2), cs2, LABEL(lexit)}, env3]
                    esac
                  esac
                esac
              esac
        | While(x, a) -> 
              case genLabels(env, 2) of [le, ls, env1] ->
                case compile(a, env1) of [cs1, env2] ->
                  case compile(x, env2) of [ce, env2] ->
                    [{JMP(le), LABEL(ls), cs1, LABEL(le), ce, CJMP("nz", ls)}, env2]
                  esac
                esac
              esac
        | Repeat(a, x) ->
              case genLabels(env, 1) of [ls, env1] ->
                case compile(a, env1) of [cs1, env2] ->
                  case compile(x, env2) of [ce, env2] ->
                    [{LABEL(ls), cs1, ce, CJMP("z", ls)}, env2]
                  esac
                esac
              esac
        | Ignore(state) -> case compile(state, env) of [cs1, env1] ->
                              [{cs1, DROP}, env1]
                            esac
        | Binop(op, lhs, rhs) -> case compile(lhs, env) of [cs1, env1] ->
                                case compile(rhs, env1) of [cs2, env2] ->
                                  [{cs1, cs2, BINOP(op)}, env2]
                                esac
                              esac
      esac
  }
  
  deepFlatten(compile(stmt, initCompEnv())[0])
  }
